/*THIS DO-FILE OPENS THE DATA FILE CALLED
analysis.dta THAT IS GENERATED BY THE
DO-FILE CALLED processing.do,

THEN GENERATES THE SIX FIGURES THAT ARE 
DISCUSSED IN THE NARRATIVE PORTION OF THIS
EXERCISE.
*/

*WHEN YOU RUN THIS DO-FILE, MAKE SURE THAT
**********1) STATA'S WORKING DIRECTORY IS SET 
*************TO THE "Command-Files" FOLDER
**********2) THE analysis.dta DATA FILE IS 
*************SAVED IN THE "Analysis-Data" FOLDER



clear
set more off

*START BY OPENING THE analysis.dta
*DATA FILE
use ../Analysis-Data/analysis.dta



*THIS COMMAND GENERATES FIGURE 1

graph bar drunk, over(free, relabel(1 "Not in Alcohol-Free Housing" 2 "Lives in Alcohol-Free Housing")) ///
	ytitle("Proportion drunk 3 or more times in past 30 days") ///
	title("Students in Alcohol-Free versus not Alcohol-Free Housing") ///
	caption("Figure 1") ///
	saving(../Graphs/Figure1.gph, replace) 
	
/*You were not expected to use all the options that are shown in the preceding command.
It would have been sufficient just to use this command:

	graph bar drunk, over(free) saving(../Graphs/Figure1.gph)
	
as shown in the assignment.*/
	

*THIS COMMAND GENERATES FIGURE 2   

graph bar drunk, over(volfree, relabel(1 "Not Voluntarily" 2 "Voluntarily")) ///
	ytitle("Proportion drunk 3 or more times in past 30 days") ///
	title("Students in Alcohol-Free Housing" "By Choice versus Assignment") ///
	caption("Figure 2") ///
	saving(../Graphs/Figure2.gph, replace) 

	/*It would have been sufficient just to use this command:
	
		graph bar drunk, over(volfree) saving(../Graphs/Figure2.gph)
	*/


*THIS COMMAND GENERATES FIGURE 3

/*For this figure, it would have been sufficient to use this command:

	graph bar drunk, over(housing) saving(../Graphs/Figure3.gph)

But in this case the long labels on the bars of the figure overlap and are
impossible to read.  That is why the suboption "label(angle(15))" is used in the 
command below.*/

graph bar drunk, over(housing, label(angle(15))) ///
	ytitle("Proportion drunk 3 or more times in past 30 days") ///
	title("Drinking by Students in All Housing Types") ///
	caption("Figure 3") ///
	saving(../Graphs/Figure3.gph, replace)
	
/*Another way to deal with the overlapping bar labels is to make a horizontal
bar graph, which you do simply by usung the command "graph hbar" instead of 
"graph bar", as illustrated below:*/

graph hbar drunk, over(housing) ///
	ytitle("Proportion drunk 3 or more times in past 30 days") ///
	title("Drinking by Students in All Housing Types") ///
	caption("Figure 3a") ///
	saving(../Graphs/Figure3a.gph, replace)

*THIS COMMAND GENERATES FIGURE 4

/*The following would have been sufficient:

	graph bar drunk, over(free) by(hsdrunk) ///
		saving(../Graphs/Figure4.gph)		
*/

/*Here is a command with options that create labels.*/
		
graph bar drunk, over(free, label(angle(15)) relabel(1 "Not in Alcohol-Free Housing" 2 "Lives in Alcohol-Free Housing")) ///
	over(hsdrunk, relabel(1 "Low HS Drinking" 2 "High HS Drinking")) ///
	ytitle("Proportion drunk 3 or more times in past 30 days") ///
	title("Students in Alcohol-Free versus not Alcohol-Free Housing" "Controlling for HS Drinking") ///
	caption("Figure 4") ///
	saving(../Graphs/Figure4.gph, replace)
	
/*Notice that when all the options are added, it turns out to work better if
we use "over-over syntax" with the "graph bar" command--i.e., the basic command
we use is

	graph bar drunk, over(free) over(hsdrunk)
	
rather than the "over-by syntax"--i.e.

	graph bar drunk, over(free) by(hsdrunk)
	
To get an idea of what the difference is between these two syntaxes,
try generating the bar graphs using both of them.  Do them both with no options,
and then do them both with all the options used in the command above that
generates Figure 4.  For each pair of figures you create (one pair without options
and one pair with options), compare what you get with the over-over syntax
to what you get with the over-by syntax.*/
	
		
*THIS COMMAND GENERATES FIGURE 5

/*The following would have been sufficient:

	graph bar drunk, over(volfree) by(hsdrunk) ///
		saving(../Graphs/Figure5.gph)	
*/

/*Here is a command with options that create labels.  Notice that we again
use the over-over syntax instead of the over-by syntax.*/

graph bar drunk, over(volfree, relabel(1 "Assigned" 2 "Choice")) ///
	over(hsdrunk, relabel(1 "Low HS Drinking" 2 "High HS Drinking")) ///
	ytitle("Proportion drunk 3 or more times in past 30 days") ///
	title("Students in Alcohol-Free Housing By Choice versus Assignment" "Controlling for HS Drinking") ///
	caption("Figure 5") ///
	saving(../Graphs/Figure5.gph, replace)
	

*THIS COMMAND GENERATES FIGURE 6

/*The following would have been sufficient:

	graph bar drunk, over(housing) by(hsdrunk) ///
		saving(../Graphs/Figure6.gph)	
*/

/*Here is a command with options that create labels.  Notice that we again
use the over-over syntax instead of the over-by syntax.*/

graph bar drunk, over(housing, label(angle(15))) ///
	over(hsdrunk, relabel(1 "Low HS Drinking" 2 "High HS Drinking")) ///
	ytitle("Proportion drunk 3 or more times in past 30 days") ///
	title("Students in All Housing Types" "Controlling for HS Drinking") ///
	caption("Figure 6") ///
	saving(../Graphs/Figure6.gph, replace)


